if(NOT GIT_EXECUTABLE)
  find_package(Git)
endif()

if(GIT_EXECUTABLE)
  execute_process(
    COMMAND ${GIT_EXECUTABLE} describe --tags --dirty --match "v*"
    WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
    OUTPUT_VARIABLE GIT_DESCRIBE_VERSION
    RESULT_VARIABLE GIT_DESCRIBE_ERROR_CODE
    OUTPUT_STRIP_TRAILING_WHITESPACE
  )
  if(NOT GIT_DESCRIBE_ERROR_CODE)
    string(REGEX REPLACE "^v" "" GITVERSIONDETECT_VERSION "${GIT_DESCRIBE_VERSION}")
  endif()
endif()

if(NOT DEFINED GITVERSIONDETECT_VERSION)
  set(GITVERSIONDETECT_VERSION "0.0.0-0-unknown")
  message(WARNING
    "Failed to determine GITVERSIONDETECT_VERSION from Git tags."
    "Using default version \"${GITVERSIONDETECT_VERSION}\"."
  )
endif()

string(REGEX
  MATCH "^([0-9]+)\\.([0-9]+)\\.([0-9]+)(-([0-9]+)-([a-z0-9]+))?"
  GITVERSIONDETECT_VERSION_MATCH
  ${GITVERSIONDETECT_VERSION}
)
set(GITVERSIONDETECT_VERSION_MAJOR ${CMAKE_MATCH_1})
set(GITVERSIONDETECT_VERSION_MINOR ${CMAKE_MATCH_2})
set(GITVERSIONDETECT_VERSION_PATCH ${CMAKE_MATCH_3})
set(GITVERSIONDETECT_VERSION_COMMIT_NUM ${CMAKE_MATCH_5})
set(GITVERSIONDETECT_VERSION_COMMIT_SHA ${CMAKE_MATCH_6})
