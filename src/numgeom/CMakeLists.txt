set(PUBLIC_HEADERS
  include/numgeom/circularlist.h
  include/numgeom/classificate.h
  include/numgeom/enums.h
  include/numgeom/ijk.h
  include/numgeom/iterator.h
  include/numgeom/loadfromvtk.h
  include/numgeom/octree.h
  include/numgeom/quadtree.h
  include/numgeom/removefaces.h
  include/numgeom/staticjaggedarray.h
  include/numgeom/tesselate.h
  include/numgeom/trimesh.h
  include/numgeom/trimeshconnectivity.h
  include/numgeom/utilities.h
)

set(SOURCE_FILES
  ${PUBLIC_HEADERS}
  classificate.cpp
  curvemetriclength.cpp     curvemetriclength.h
  ffextintersections.cpp    ffextintersections.h
  intersections.h           intersections.cpp
  iterator_ijk.cpp          iterator_ijk.h
  loadfromvtk.cpp
  octree.cpp
  quadtree.cpp
  removefaces.cpp
  staticjaggedarray.cpp
  tesselate.cpp
  trimesh.cpp
  trimeshconnectivity.cpp
  utilities.cpp
  writevtk.cpp              writevtk.h
)

add_library(numgeom ${SOURCE_FILES})
add_library(numgeom::numgeom ALIAS numgeom)

set_target_properties(numgeom PROPERTIES PUBLIC_HEADER "${PUBLIC_HEADERS}")

generate_export_header(numgeom
  EXPORT_FILE_NAME ${CMAKE_CURRENT_BINARY_DIR}/include/numgeom/numgeom_export.h
)

if(FLEX_FOUND AND BISON_FOUND)
  flex_target(vtk_scanner vtk.l ${CMAKE_CURRENT_BINARY_DIR}/vtk_scanner.c)
  bison_target(vtk_parser vtk.y ${CMAKE_CURRENT_BINARY_DIR}/vtk_parser.c)
  add_flex_bison_dependency(vtk_scanner vtk_parser)

  target_sources(numgeom
    PRIVATE
      ${BISON_vtk_parser_OUTPUTS}
      ${FLEX_vtk_scanner_OUTPUTS}
  )

  target_include_directories(numgeom
    PRIVATE
      ${CMAKE_CURRENT_BINARY_DIR}
  )
else()
  target_sources(numgeom
    PRIVATE
      ${CMAKE_CURRENT_SOURCE_DIR}/autogenerated/vtk_parser.h
      ${CMAKE_CURRENT_SOURCE_DIR}/autogenerated/vtk_parser.c
      ${CMAKE_CURRENT_SOURCE_DIR}/autogenerated/vtk_scanner.c
  )

  target_include_directories(numgeom
    PRIVATE
      ${CMAKE_CURRENT_SOURCE_DIR}/autogenerated
  )
endif()

target_link_libraries(numgeom
  PUBLIC
    numgeom::core
    TKMath
    TKG3d
    TKernel
  PRIVATE
    TKBO
    TKBRep
    TKG2d
    TKDESTEP
    TKGeomAlgo
    TKGeomBase
    TKMesh
    TKShHealing
    TKTopAlgo
    TKXSBase
)

target_include_directories(numgeom
  PUBLIC
    ${CMAKE_CURRENT_SOURCE_DIR}/include
    ${CMAKE_CURRENT_BINARY_DIR}/include
)
